/-
Copyright (c) 2025 Devon Tuma. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Devon Tuma, Quang Dao
-/
import VCVio.EvalDist.Defs.NeverFails
import VCVio.EvalDist.Monad.Map

/-!
# Evaluation Distributions on Boolean-Valued Computations

Specialization lemmas for `MonadLiftT m SPMF` computations returning `Bool`.
-/

variable {m : Type _ → Type _} [Monad m] [MonadLiftT m SPMF] {α β : Type _}

omit [Monad m] in
@[simp, grind =]
lemma probOutput_true_add_false (mx : m Bool) :
    Pr[= true | mx] + Pr[= false | mx] = 1 - Pr[⊥ | mx] := by
  simpa using tsum_probOutput_eq_sub mx

omit [Monad m] in
@[simp, grind =]
lemma probOutput_false_add_true (mx : m Bool) :
    Pr[= false | mx] + Pr[= true | mx] = 1 - Pr[⊥ | mx] := by
  rw [add_comm, probOutput_true_add_false]

omit [Monad m] in
lemma probOutput_true_eq_sub (mx : m Bool) :
    Pr[= true | mx] = 1 - Pr[⊥ | mx] - Pr[= false | mx] := by
  rw [← probOutput_true_add_false]
  exact (ENNReal.add_sub_cancel_right probOutput_ne_top).symm

omit [Monad m] in
lemma probOutput_false_eq_sub (mx : m Bool) :
    Pr[= false | mx] = 1 - Pr[⊥ | mx] - Pr[= true | mx] := by
  rw [← probOutput_false_add_true]
  exact (ENNReal.add_sub_cancel_right probOutput_ne_top).symm

@[simp]
lemma probOutput_not_map [LawfulMonad m] [LawfulMonadLiftT m SPMF] (mx : m Bool) :
    Pr[= true | (! ·) <$> mx] = Pr[= false | mx] :=
  probOutput_map_injective mx (fun a b h => by cases a <;> cases b <;> simp_all) false

@[simp]
lemma probOutput_not_map' [LawfulMonad m] [LawfulMonadLiftT m SPMF] (mx : m Bool) :
    Pr[= false | (! ·) <$> mx] = Pr[= true | mx] :=
  probOutput_map_injective mx (fun a b h => by cases a <;> cases b <;> simp_all) true

@[grind =]
lemma probOutput_true_add_false_of_neverFail {mx : m Bool} [NeverFail mx] :
    Pr[= true | mx] + Pr[= false | mx] = 1 := by simp

omit [Monad m] in
@[simp, grind =]
lemma probEvent_true_eq_probOutput (mx : m Bool) :
    Pr[ (· = true) | mx] = Pr[= true | mx] := probEvent_eq_eq_probOutput mx true

omit [Monad m] in
@[simp, grind =]
lemma probEvent_not_eq_probOutput (mx : m Bool) :
    Pr[ (· = false) | mx] = Pr[= false | mx] := probEvent_eq_eq_probOutput mx false

lemma probOutput_true_bind_map_eq_probEvent [LawfulMonad m] [LawfulMonadLiftT m SPMF]
    (mx : m α) (my : α → m β) (p : α → β → Bool) :
    Pr[= true | mx >>= fun x => p x <$> my x] =
      Pr[fun (x, y) => p x y | do let x ← mx; return (x, ← my x)] := by
  simp only [probOutput_bind_eq_tsum, probOutput_map_eq_tsum_ite, Bool.true_eq, bind_pure_comp,
    probEvent_bind_eq_tsum, probEvent_map]
  grind
