/-
Copyright (c) 2026 Quang Dao. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Quang Dao
-/

import Lean.Elab.Tactic
import Lean.Meta.Sym.Simp.Goal
import Lean.Meta.Sym.Simp.Rewrite
import Lean.Meta.Sym.Util
import VCVio.ProgramLogic.Tactics.Common.WpStepRegistry
import VCVio.ProgramLogic.Unary.SimulateQ

/-!
# `runWpStepRules` Dispatch

Dispatch tactic for raw `wp`-shaped goals plus the canonical `@[wpStep]`
registrations for every `wp_*` lemma shipped in
`VCVio/ProgramLogic/Unary/HoareTriple.lean` and
`VCVio/ProgramLogic/Unary/SimulateQ.lean`.

The registrations live here (rather than in the rule files themselves) because
`Unary/HoareTriple.lean` and `Unary/SimulateQ.lean` sit below the tactic
infrastructure in the import DAG, mirroring the centralized `attribute [vcspec]`
block in `VCVio/ProgramLogic/Tactics/Relational/Internals.lean`.
-/

open Lean Elab Tactic Meta

namespace OracleComp.ProgramLogic

/-! ## Canonical registrations -/

attribute [wpStep]
  -- Pure / bind / branching from `Unary/HoareTriple.lean`
  OracleComp.ProgramLogic.wp_pure
  OracleComp.ProgramLogic.wp_bind
  OracleComp.ProgramLogic.wp_ite
  OracleComp.ProgramLogic.wp_dite
  OracleComp.ProgramLogic.wp_map
  -- Replicate / list iterators
  OracleComp.ProgramLogic.wp_replicate_zero
  OracleComp.ProgramLogic.wp_replicate_succ
  OracleComp.ProgramLogic.wp_list_mapM_nil
  OracleComp.ProgramLogic.wp_list_mapM_cons
  OracleComp.ProgramLogic.wp_list_foldlM_nil
  OracleComp.ProgramLogic.wp_list_foldlM_cons
  -- Sampling / queries
  OracleComp.ProgramLogic.wp_query
  OracleComp.ProgramLogic.wp_HasQuery_query
  OracleComp.ProgramLogic.wp_uniformSample
  -- `simulateQ` / coercion-bridging from `Unary/SimulateQ.lean`
  OracleComp.ProgramLogic.wp_simulateQ_eq
  OracleComp.ProgramLogic.wp_simulateQ_run'_eq
  OracleComp.ProgramLogic.wp_liftComp

/-! ## Dispatch -/

/-- Cache of Sym rewrite theorem objects for global `@[wpStep]` declarations. -/
initialize wpStepRewriteRuleCache :
    IO.Ref (Std.HashMap Name Lean.Meta.Sym.Simp.Theorem) ←
  IO.mkRef {}

private def traceWpStepCacheEvent (event : String) (declName : Name) : MetaM Unit := do
  if vcvio.vcgen.traceCachedRules.get (← getOptions) then
    logInfo m!"[wpstep cache] {event} `{declName}`"

private def getWpStepRewriteRuleCached (declName : Name) : MetaM Lean.Meta.Sym.Simp.Theorem := do
  let cache ← wpStepRewriteRuleCache.get
  match cache[declName]? with
  | some thm =>
      addCachedRuleHit
      traceWpStepCacheEvent "hit" declName
      return thm
  | none =>
      addCachedRuleMiss
      traceWpStepCacheEvent "miss" declName
      let thm ←
        if vcvio.vcgen.time.get (← getOptions) then
          let (thm, ns) ← timeNs (Lean.Meta.Sym.Simp.mkTheoremFromDecl declName)
          addCachedRuleBuildTime ns
          return thm
        else
          Lean.Meta.Sym.Simp.mkTheoremFromDecl declName
      wpStepRewriteRuleCache.modify fun cache => cache.insert declName thm
      return thm

private def wpStepMethods (thm : Lean.Meta.Sym.Simp.Theorem) : Lean.Meta.Sym.Simp.Methods :=
  let thms := ({} : Lean.Meta.Sym.Simp.Theorems).insert thm
  { post := thms.rewrite }

/-- Apply a cached Sym rewrite theorem to the current goal target. -/
private def runWpStepRewriteRule (declName : Name) : TacticM Bool := do
  match ← getGoals with
  | [] => return false
  | goal :: rest =>
      let thm ← liftMetaM <| getWpStepRewriteRuleCached declName
      match ← observing? do
          liftMetaM <| Lean.Meta.Sym.SymM.run do
            let goal ← Lean.Meta.Sym.preprocessMVar goal
            Lean.Meta.Sym.simpGoal goal (wpStepMethods thm)
        with
      | none => return false
      | some .noProgress => return false
      | some .closed =>
          setGoals rest
          return true
      | some (.goal goal') =>
          setGoals [goal']
          discard <| tryEvalTacticSyntax (← `(tactic| rfl))
          let goals ← getGoals
          setGoals (goals ++ rest)
          return true

private def mergeWpStepCandidateEntries (preferred fallback : Array WpStepEntry) :
    Array WpStepEntry :=
  fallback.foldl (init := preferred) fun entries entry =>
    let duplicate := entries.any fun existing =>
      existing.declName? == entry.declName?
    if duplicate then entries else entries.push entry

/-- Advance a `wp`-shaped goal by one rewrite, dispatching via the `@[wpStep]`
registry.

Locates the `wp oa post` sub-expression of the main target, first asks the
registry for syntactic candidates, and then appends normalized fallback
candidates. Each candidate is tried through the cached `SymM` rewrite path.

Returns `false` when no candidate succeeds, or when the goal contains no `wp`
application at all. -/
def runWpStepRules : TacticM Bool := withVCGenWpStepTiming do
  let target ← instantiateMVars (← getMainTarget)
  let some wpApp := findAppWithHead? ``OracleComp.ProgramLogic.wp target | return false
  let wpApp ← instantiateMVars wpApp
  let argCount := wpApp.getAppNumArgs
  if argCount < 2 then return false
  let oa := wpApp.getArg! (argCount - 2)
  let entries :=
    mergeWpStepCandidateEntries
      (← getRegisteredWpStepEntriesNoWhnf oa)
      (← getRegisteredWpStepEntries oa)
  for entry in entries do
    let some declName := entry.declName? | continue
    if ← runWpStepRewriteRule declName then return true
  return false

end OracleComp.ProgramLogic
